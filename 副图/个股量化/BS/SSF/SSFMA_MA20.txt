X_1:=ABS(CLOSE-REF(CLOSE,11));         {11日收盘价  总价差             （总波动）       }
X_2:=SUM(ABS(CLOSE-REF(CLOSE,1)),11);  {最近11日    每日收盘价差-之和  （每日波动-之和）}
X_3:=ABS(X_1/X_2);                     { 总价差 / 每日价差-之和 }
X_4:=X_3*(2/N-2/M)+2/M;
X_5:=X_4*X_4;

SSF1 :   EMA(DMA(CLOSE,X_5), 2);



IF(SSF1>REF(SSF1,1),     SSF1,     DRAWNULL)     POINTDOT  COLORRED     LINETHICK4;    { > 红色 }
IF(SSF1>REF(SSF1,1),     SSF1,     DRAWNULL)               COLORRED;

IF(SSF1=REF(SSF1,1),     SSF1,     DRAWNULL)     POINTDOT  COLORWHITE   LINETHICK4;    { = 白色 }
IF(SSF1=REF(SSF1,1),     SSF1,     DRAWNULL)               COLORWHITE;

IF(SSF1<REF(SSF1,1),     SSF1,     DRAWNULL)     POINTDOT  COLORGREEN   LINETHICK4;    { < 绿色 }
IF(SSF1<REF(SSF1,1),     SSF1,     DRAWNULL)               COLORGREEN;







{ ------------------------------------------- }
MA5  := MA(C,  5) COLORWHITE  ;
MA10 := MA(C, 10) COLORYELLOW ;
MA20 :  MA(C, 20) COLORMAGENTA;
MA50 : IF(MA(C, 50)=DRAWNULL, 0, MA(C, 50))   COLORGREEN;
MA100:=IF(MA(C,100)=DRAWNULL, 0, MA(C,100));
MA150:=IF(MA(C,150)=DRAWNULL, 0, MA(C,150));
MA200:=IF(MA(C,200)=DRAWNULL, 0, MA(C,200));
MA250:=IF(MA(C,250)=DRAWNULL, 0, MA(C,250));





RPS10  :=  TOOLS_RPS.RPS10   NODRAW;
RPS20  :=  TOOLS_RPS.RPS20   NODRAW;
RPS50  :=  TOOLS_RPS.RPS50   NODRAW;
RPS120 :=  TOOLS_RPS.RPS120  NODRAW;
RPS250 :=  TOOLS_RPS.RPS250  NODRAW;



_20CM :=  _20CM._20CM     NODRAW;





NOTEXT_MA20_1 :  IF(C< MA20         ,   MA20,   DRAWNULL)     COLORBLUE    LINETHICK2;
NOTEXT_MA20_2 :  IF(MA20<REF(MA20,1),   MA20,   DRAWNULL)     COLORCYAN    LINETHICK2;

NOTEXT_MA50_1 :  IF(C< MA50         ,   MA50,   DRAWNULL)     COLORBLUE    LINETHICK3;
NOTEXT_MA50_2 :  IF(MA50<REF(MA50,1),   MA50,   DRAWNULL)     COLORCYAN    LINETHICK3;










{----------------------}
BS状态 :  BS状态.BS状态  NODRAW;

动量股 :  TOOLS.动量股   NODRAW;
趋势股 :  TOOLS.趋势股   NODRAW;
慢牛股 :  TOOLS.慢牛股   NODRAW;







{ ----------------------------------------------------------------------------------------------- }
中期调整_N := IF(MA多(100) || MA多(200),   100,   200);


_H    :=  HHV(H,     中期调整_N);
H_DAY :=  HHVBARS(H, 中期调整_N)   +1;

_L    :=  LLV(L, H_DAY);
L_DAY :=  BARSLAST(L=_L)  +1;


调整幅度  : _L/_H *100 -100     COLORCYAN NODRAW;
调整天数  : H_DAY - L_DAY       COLORCYAN NODRAW;
调整幅度2 :  L/_H *100 -100     COLORCYAN NODRAW;
调整天数2 : H_DAY -1            COLORCYAN NODRAW;


DRAWNUMBER(CURRBARSCOUNT=CONST(L_DAY),   _L      ,   调整幅度)   COLORCYAN;
DRAWNUMBER(CURRBARSCOUNT=CONST(L_DAY),   _L *0.97,   调整天数)   COLORCYAN;







{ ---------------- 提前大盘止跌 }







{ ---------------- }
DRAWTEXT_FIX(显示板块,0.01,0.05,0,HYBLOCK),COLORYELLOW;
DRAWTEXT_FIX(显示板块,0.01,0.10,0,GNBLOCK),COLORYELLOW;
DRAWTEXT_FIX(显示板块,0.01,0.15,0,ZDBLOCK),COLORWHITE;













{ ------------------------------------------- }
AMO_MA5    :=  MA(AMO, 5)   NODRAW;
AMO_MA10   :=  MA(AMO,10)   NODRAW;
涨停       :=  C/REF(C,1)>=1.098 AND C=H;
AMO_VAL    :=  IF(涨停, MAX(AMO, AMO_MA10), AMO)   NODRAW;

RPS_DAY    :=  IF(RPS250=0,  BARSLAST(RPS50<50),  BARSLAST(ABS(RPS50<50)+ABS(RPS120<50)+ABS(RPS250<50) >=2))   NODRAW;   {上次 RPS双线<50  距今天数}
AMO_起涨前 :=  REF(SUM(AMO_MA10,10)/10, RPS_DAY)  NODRAW;


AMO_BL     :  AMO_VAL / AMO_起涨前        COLORRED NODRAW;
AMO_BL2    :  AMO_VAL / REF(AMO_MA10,10)  COLORRED NODRAW;      {  当日成交额  /  10日前的  MA10_成交额  }
{ ------------------------------------------- }







{ ------------------------------ 0、指标统计 输出 -------------------------------- }

中期涨幅_MA20  :  中期涨幅N(20)     COLORMAGENTA NODRAW;
中期天数_MA20  :  中期天数N(20)     COLORMAGENTA NODRAW;


中期涨幅_MA50  :  中期涨幅N(50)     COLORGREEN   NODRAW;
中期天数_MA50  :  中期天数N(50)     COLORGREEN   NODRAW;





中期涨幅     :   中期涨幅.中期涨幅   COLORRED NODRAW;

中期涨幅_H   :=  HHV(中期涨幅, 10);
中期涨幅_H20 :=  HHV(中期涨幅, 20);
中期涨幅_H50 :=  HHV(中期涨幅, 50);


中期天数     :   中期涨幅.中期天数   COLORRED NODRAW;
中期均线     :   中期涨幅.中期均线   COLORRED NODRAW;





{C_MA20比率 :=  C/MA20   COLORRED NODRAW;}


C新高 :  SUMBARS(REF(C,1)-C,   C*0.0001)     COLORGREEN NODRAW;
H新高 :  SUMBARS(REF(H,1)-H,   H*0.0001)     COLORGREEN NODRAW;








{----------------------}
涨幅_H := H/REF(C,1) *100-100;
振幅   := H/L *100-100 NODRAW;


上影线_比例 :  (C-L)/(H-L)     COLORGREEN NODRAW;
上影线      :  (C-L)/(H-L) < 0.4     AND     (振幅>=HHV(振幅,10)*0.7 || IF(_20CM, 振幅>10, 振幅>5)),COLORGREEN NODRAW;   { 收盘价 位于振幅 下1/3}




{   ----- 1、CON_月多 -----   }

月多     :    TOOLS.月多     COLORYELLOW NODRAW;
周多     :    TOOLS.周多     COLORYELLOW NODRAW;




{ SAR周多  -  C > SAR#周 }
SAR周多:  C>=SAR.SAR#WEEK ,COLORMAGENTA NODRAW;
DRAWTEXT(BARSSINCEN(SAR周多,   15)=0 , 0.98*L,  'SAR多') COLORYELLOW;
DRAWTEXT(BARSSINCEN(SAR周多=0, 10)=0 , 1.02*H,  'SAR空') COLORCYAN;



MACD日多 := MACD.MACD>=0;
 SAR日多 := C>=SAR.SAR;


CON_月日_多:=     月多 AND SAR日多;

RPS三线红       :=  RPS50>=RPS AND RPS120>=RPS AND RPS250>=RPS;
CON_月多_三线红 :=        月多 AND RPS三线红,  COLORRED NODRAW;


STICKLINE(BARSSINCEN(    月多,          2)=0   ,0,1  ,0,3) COLORGREEN;
STICKLINE(BARSSINCEN(CON_月日_多,       2)=0   ,0,1  ,0,3) COLORMAGENTA;
STICKLINE(BARSSINCEN(CON_月多_三线红,   2)=0   ,0,1  ,0,3) COLORYELLOW;

STICKLINE(BARSSINCEN(CON_月多_三线红=0, 2)=0   ,0,1  ,0,3) COLORMAGENTA;
STICKLINE(BARSSINCEN(CON_月日_多=0,     2)=0   ,0,1  ,0,3) COLORBLUE;
STICKLINE(BARSSINCEN(    月多=0,        2)=0   ,0,1  ,1,3) COLORCYAN;









{ -----------------  均线萌出  ----------------- }
均线萌出 :  TOOLS.均线萌出     COLORWHITE NODRAW;
{
STICKLINE(BARSSINCEN(均线萌出,   20)=0,   LLV(L,200), 2*HHV(H,200)   ,0,-1)     COLORWHITE;
};
