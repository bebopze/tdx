SSF_MA20多 :   SSF多  AND  MA多(20)     AND     SSF1>=MA(C,20)       COLORRED;










{ --------------------------------------------------------------------------------------------------- }
TESTSKIP(X);





_H := 1 * 0.9;
_M := 1 * 0.5;
_L := 1 * 0.1;





{ --------------------------------------------------------------------------------------------------- }

中期涨幅   :=   中期涨幅N(20);
中期涨幅_H :=   HHV(中期涨幅,10);



{ --------------- }

涨 :=   SSF_MA20多;
跌 :=   涨=0;


B :=   涨  AND  REF(跌,1);
S :=   跌  AND  REF(涨,1);




{  ----------  持股/空仓  }

持股天数    :  IF(涨||S,BARSLAST(B),0)   ,  COLORRED NODRAW;
买点涨幅    :  C/REF(C,持股天数) *100-100,  COLORRED NODRAW;
买点涨幅_H  := H/REF(C,持股天数) *100-100,  COLORRED NODRAW;

B_MIN :  IF(持股天数>0, LLV(买点涨幅  , 持股天数), 0),  COLORFF00FF NODRAW;
B_MAX :  IF(持股天数>0, HHV(买点涨幅  , 持股天数), 0),  COLORFF00FF NODRAW;
BH_MAX:= IF(持股天数>0, HHV(买点涨幅_H, 持股天数), 0),  COLORFF00FF NODRAW;



{
空仓天数 :  IF(跌,   BARSLAST(S),0)    ,  COLORGREEN NODRAW;
卖点跌幅 :  C/REF(C,空仓天数) *100-100 ,  COLORGREEN NODRAW;
}
空仓天数 :  IF(跌,  IF(BARSLAST(S)=DRAWNULL, BARSCOUNT(C)+1, BARSLAST(S))  ,0)                    ,COLORGREEN NODRAW;
卖点跌幅 :  IF(BARSLAST(S)=DRAWNULL ,  C/REF(C,空仓天数-1) *100-100, C/REF(C,空仓天数) *100-100)  ,COLORGREEN NODRAW;





{  ----------  收益率  }

上市天数  : BARSCOUNT(C) +1 ,COLORC0C0C0 NODRAW;
距今天数  : CURRBARSCOUNT-1 ,COLORC0C0C0 NODRAW;


统计N天   : N               ,COLORC0C0C0 NODRAW;

N累计持股 : COUNT(涨,N)     ,COLORFF00FF  NODRAW;
N累计空仓 : COUNT(跌,N)     ,COLOR808000  NODRAW;





{  时间段内   所有 S点-买点涨幅（含 当前-最后一次 还在ING中的涨幅）  之 复利（乘积）  }

{ING: 涨 AND BARSNEXT(S),COLORRED  NODRAW;}

S_涨幅       := IF(S || (涨 AND CURRBARSCOUNT=1),  买点涨幅/100,  0), COLORRED NODRAW;  {2->0.02}

N累计涨幅    :  MULAR(1+S_涨幅 ,N) *100-100, COLORRED    NODRAW;
N累涨MAX     :  HHV(N累计涨幅  ,N)         , COLORFF00FF NODRAW;





近1年  :  MULAR(1+S_涨幅, 250*1) *100-100  ,  COLOR00FFFF   NODRAW;
近2年  :  MULAR(1+S_涨幅, 250*2) *100-100  ,  COLOR4080FF   NODRAW;
近3年  :  MULAR(1+S_涨幅, 250*3) *100-100  ,  COLOR008080   NODRAW;

近5年  :  MULAR(1+S_涨幅, 250*5) *100-100  ,  COLORC08080   NODRAW;
近10年 :  MULAR(1+S_涨幅, 250*10)*100-100  ,  COLOR808040   NODRAW;














{----------------------------------------------------------------------------- K线 }

{  ----------  止盈/止损 }

MA5_MA10_死叉     :=  REF(MA5,1)>REF(MA10,1)  AND  MA5<=MA10;
下穿MA10          :=  REF(C  ,1)>REF(MA10,1)  AND  C  <=MA10;
回撤_破MA5        :=  BH_MAX-买点涨幅>=10     AND  C  < MA5 ;
冲顶回撤_长上影   :=  BH_MAX-买点涨幅>=13     AND  H/C >=1.13;
大阴线_收益减半   :=  买点涨幅/BH_MAX<=0.6;
强势卖出          :=  中期涨幅_H>=100         OR   (  (中期涨幅_H>=90 || BH_MAX>=50)  AND  (C<MA5 || H/C>=1.05)   )  NODRAW;

DRAWICON(涨 AND B_MAX>=30   AND   (MA5_MA10_死叉 || 下穿MA10 || 回撤_破MA5 || 冲顶回撤_长上影 || 大阴线_收益减半  ||  强势卖出),   1*_H,  8);  { 盈利30%  -  考虑止盈 }





{  ----------  盈亏/标识 }
DRAWTEXT( ((涨 AND CURRBARSCOUNT=1) || S)   AND   S_涨幅>=   0,     _H,     VAR2STR(买点涨幅,0))     COLORMAGENTA;
DRAWTEXT( ((涨 AND CURRBARSCOUNT=1) || S)   AND   S_涨幅>=0.50,     _H,     VAR2STR(买点涨幅,0))     COLORRED;
DRAWTEXT( ((涨 AND CURRBARSCOUNT=1) || S)   AND   S_涨幅<    0,     _H,     VAR2STR(买点涨幅,0))     COLORCYAN;

DRAWTEXT( ((涨 AND CURRBARSCOUNT=1) || S)                     ,     0.7*_H,     VAR2STR(持股天数,0))     COLORMAGENTA;



DRAWTEXT( ((跌 AND CURRBARSCOUNT=1) || B)   AND   REF(卖点跌幅,1)>=   0,     _M,     VAR2STR(IF(CURRBARSCOUNT=1, 卖点跌幅, REF(卖点跌幅,1)),0))     COLORMAGENTA;
DRAWTEXT( ((跌 AND CURRBARSCOUNT=1) || B)   AND   REF(卖点跌幅,1)>= -10,     _M,     VAR2STR(IF(CURRBARSCOUNT=1, 卖点跌幅, REF(卖点跌幅,1)),0))     COLORGREEN;
DRAWTEXT( ((跌 AND CURRBARSCOUNT=1) || B)   AND   REF(卖点跌幅,1)<  -10,     _M,     VAR2STR(IF(CURRBARSCOUNT=1, 卖点跌幅, REF(卖点跌幅,1)),0))     COLORCYAN;

DRAWTEXT( ((跌 AND CURRBARSCOUNT=1) || B)                              ,     0.7*_M,     VAR2STR(IF(CURRBARSCOUNT=1, 空仓天数, REF(空仓天数,1)),0))     COLORCYAN;





H_ZF10        :=  涨 AND H=HHV(H,持股天数) AND 买点涨幅_H>=10                  NODRAW;
ZF10_N        :=  买点涨幅_H/10 - FRACPART(买点涨幅_H/10)                      NODRAW;
CON_H_ZF10_N  :=  H_ZF10  AND  MOD(ZF10_N,2)=0  AND  ZF10_N-REF(ZF10_N,1)>=1   NODRAW;

DRAWTEXT(CON_H_ZF10_N  AND CURRBARSCOUNT>1,     _H,     VAR2STR(BH_MAX, 0))       COLORMAGENTA;
