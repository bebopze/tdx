大盘 :  IFC(TOOLS_RPS.大盘,  1,  TESTSKIP(1))     NODRAW;





{----------------------------------------------------------------------}
MA5  := MA(C,5)    COLORWHITE;
MA10 := MA(C,10)   COLORYELLOW;
MA20 := MA(C,20)   COLORMAGENTA;
MA50 := IF(MA(C, 50)=DRAWNULL,  0,  MA(C, 50))   COLORGREEN ;
MA100:= IF(MA(C,100)=DRAWNULL,  0,  MA(C,100))   COLORLIGRAY;
MA200:= IF(MA(C,250)=DRAWNULL,  0,  MA(C,250))   COLORBLUE  ;
MA250:= IF(MA(C,250)=DRAWNULL,  0,  MA(C,250));






{ -------------------------------------------------------------- 第1版（2024-4-17）}


MA50极值_VAL  :  IF(一二阶段,   25,   IF(四阶段,   15,   20))   NODRAW;
MA50高位_VAL  :  IF(一二阶段,   75,   IF(四阶段,   60,   65))   NODRAW;



MA50极冰  :  MA50占比.上MA50占比% <= 5          NODRAW;



MA50极值  :  MA50占比.上MA50占比% < MA50极值_VAL   NODRAW;
新低极值  :  新高_新低.差值 < -150                 NODRAW;

MA50高位  :  MA50占比.上MA50占比% > MA50高位_VAL   NODRAW;

中继下跌  :  COUNT(MA50占比.上MA50占比% <50,   50) >= 45 NODRAW;


新高_正   :=  新高_新低.差值 > 0          NODRAW;
新高_负   :=  新高_新低.差值 < 0          NODRAW;



放量      :   AMO>MA(AMO,5)  ||  AMO>MA(AMO,10)    NODRAW;







MA微多头 :=  C>MA5 AND MA5>MA10                 AND     MA5>REF(MA5,1) AND MA10>REF(MA10,1)                           NODRAW;

MA多头   :=  C>MA5 AND MA5>MA10 AND MA10>MA20   AND     MA5>REF(MA5,1) AND MA10>REF(MA10,1)  AND MA20>REF(MA20,1)     NODRAW;
MA大多头 :=  MA多头             AND MA20>MA50   AND     MA50>REF(MA50,1)                                              NODRAW;


MA转空1  :=  C<MA5 AND C<MA10                   AND   ( MA5<REF(MA5,1) AND MA10<REF(MA10,1)                       )   NODRAW;
MA转空2  :=  C<MA5 AND C<MA10 AND C<MA20        AND   (                    MA10<REF(MA10,1)  OR  MA20<REF(MA20,1) )   NODRAW;
MA转空   :=  MA转空1 || MA转空2   NODRAW;


MA空头   :=  C<MA5 AND C<MA10 AND C<MA20        AND   ( MA5<REF(MA5,1) AND MA10<REF(MA10,1)                       )   NODRAW;
MA大空头 :=  C<MA5 AND C<MA10 AND C<MA20        AND   ( MA5<REF(MA5,1) AND MA10<REF(MA10,1)  AND MA20<REF(MA20,1) )   NODRAW;


涨幅  :=  C/REF(C,1)*100-100;
C振幅 :=  C/L*100-100;



大盘爆量     :  "880003$AMO">=10000* 100000000   NODRAW;  { 大盘-万亿 }
大盘爆量大阳 :  大盘爆量                         AND   (涨幅=HHV(涨幅,10)  ||  涨幅>=1.5  ||  (C振幅>=1.1 AND 涨幅>=0.8)   )   NODRAW;



中期ZF_左侧试仓 :=  IF(TOOLS_RPS.A股,  30,   IF(TOOLS_RPS.板块,  25,   IF(TOOLS_RPS.大盘,  20,  -1)));
中期ZF_左侧买   :=  IF(TOOLS_RPS.A股,  40,   IF(TOOLS_RPS.板块,  30,   IF(TOOLS_RPS.大盘,  25,  -1)));
中期ZF_右侧买   :=  IF(TOOLS_RPS.A股,  50,   IF(TOOLS_RPS.板块,  35,   IF(TOOLS_RPS.大盘,  30,  -1)));



左侧试仓 :  放量   AND   中期涨幅<中期ZF_左侧试仓       AND
            (
                ( COUNT(MA50极冰, 10)>=1   AND   COUNT(新低极值, 5)>=1   AND   COUNT(涨幅>=1.5 || (C振幅>=1.3 AND 涨幅>=0.8), 3)   AND   C>=MA5   )          { 极度冰点 }

            ||  ( COUNT(MA50极值, 10)>=5   AND   COUNT(新低极值, 5)>=1   AND   COUNT(涨幅>=1.5 || (C振幅>=1.3 AND 涨幅>=0.8), 3)   AND   C>=MA5   )          { 冰点     }

            ||  ( COUNT(MA50极值, 10)>=3   AND   MA50>=REF(MA50,1)       AND   COUNT(涨幅>=1.5 || (C振幅>=1.3 AND 涨幅>=0.8), 3)   AND   C>=MA5   )          { 震荡     }



            ||  ( 中继下跌                 AND   COUNT(新低极值, 5)>=1   AND   COUNT(涨幅>=1.5 || (C振幅>=1.3 AND 涨幅>=0.8), 3)   AND   C>=MA5   )          { 中继下跌 }

            ) COLORWHITE   NODRAW;





左侧买1  :    COUNT(左侧试仓, 3)     AND     放量   AND   中期涨幅 < 中期ZF_左侧买   AND   MA多(5)                                              COLORGREEN NODRAW;       { 股灾 - 买点 }
左侧买2  :    MA多(50)               AND     放量   AND   中期涨幅 < 中期ZF_左侧买   AND   大盘爆量大阳   AND   新高_正  AND  COUNT(新高_负,3)  COLORGREEN NODRAW;       { 震荡 - 买点 }

左侧买 :  左侧买1 || 左侧买2          COLORGREEN   NODRAW;







右侧买1  :  COUNT(左侧买1, 15)   AND   COUNT(新高_正,10)>=6   AND   新高_正   AND   放量                    AND   (   MA微多头 || MA多头 ||  C>=MA50   )  COLORMAGENTA NODRAW;       { 股灾 - 买点 }
右侧买2  :  COUNT(左侧买2, 15)   AND                                新高_正   AND   放量   AND   大盘爆量   AND   (               MA多头 AND C>=MA50   )  COLORMAGENTA NODRAW;       { 震荡 - 买点 }
           


上半年线 := C>MA100 || C>MA200;

右侧买3  :  新高_正   AND   
            (
                    (周多      AND   中期涨幅 < 中期ZF_右侧买   AND   MA多头   AND   上半年线)
	         ||     (月多      AND   中期涨幅 < 中期ZF_右侧买   AND   MA多头)
             ||     (均线萌出  AND   中期涨幅 < 中期ZF_右侧买               )

		     ||     (MA大多头  AND   中期涨幅 < 中期ZF_右侧买   AND   放量  ) 
           )        COLORMAGENTA NODRAW;




一二阶段_右侧买 :  一二阶段 AND 1     COLORMAGENTA NODRAW;   { 大牛市 - 年级    TODO }




右侧买4 :=   COUNT(左侧买1, 25)   AND   COUNT(右侧买1, 15)   AND   MA多头;    { 有待商榷 ... }


右侧买 : 右侧买1  ||  右侧买2  ||  右侧买3     COLORMAGENTA NODRAW;




{}







大盘MA50高位 := MA50占比.上MA50占比%>=65;





强势卖出 : 大盘MA50高位   AND   MA多头     AND   COUNT(新高_正,15)>=12     COLORRED NODRAW;





左侧卖1 :=    COUNT(大盘MA50高位,  10)     AND     COUNT(MA多头, 20)   AND   MA转空     COLORBLUE NODRAW;      { 中期涨幅 - 到顶 转空 }
左侧卖2 :=    MA空头;                                                                                          { 震荡市   -      转空 }

左侧卖  :     左侧卖1  ||  左侧卖2     COLORBLUE NODRAW;





右侧卖1 :=  COUNT(大盘MA50高位,  15)   AND   
           (
		        (  COUNT(MA多头, 20)   AND   MA空(20)  )
	       ||   (  COUNT(MA多头, 30)   AND   MA空头    )
           );                                                          { 中期涨幅 - 到顶 转大空头 }


右侧卖2 :=    MA大空头;                                                { 震荡市   -        大空头 }


右侧卖  :  右侧卖1  ||  右侧卖2     COLORCYAN NODRAW;










BS_STATUS_0  :  IF(右侧卖, 6,   IF(左侧卖, 5,   IF(强势卖出, 4,   IF(右侧买, 3,   IF(左侧买, 2,   IF(左侧试仓, 1,   0))))))     COLORWHITE   NODRAW;
BS_STATUS_1  :  IF(BS_STATUS_0>0,   BS_STATUS_0,   REF(BS_STATUS_0,  BARSLAST(BS_STATUS_0 > 0))   )                             COLORMAGENTA NODRAW;

BS_STATUS    :  IF(COUNT(BS_STATUS_1=6, 3)  AND  BS_STATUS_1=5,   6,
                IF(REF(BS_STATUS_1,1)=3     AND  BS_STATUS_1<3,   3,

                BS_STATUS_1))     COLORWHITE;





NOTEXT_BS状态_1 :  IF(BS_STATUS=1,  BS_STATUS,  DRAWNULL)     COLORWHITE;
NOTEXT_BS状态_2 :  IF(BS_STATUS=2,  BS_STATUS,  DRAWNULL)     COLORGREEN;
NOTEXT_BS状态_3 :  IF(BS_STATUS=3,  BS_STATUS,  DRAWNULL)     COLORMAGENTA;
NOTEXT_BS状态_4 :  IF(BS_STATUS=4,  BS_STATUS,  DRAWNULL)     COLORRED;
NOTEXT_BS状态_5 :  IF(BS_STATUS=5,  BS_STATUS,  DRAWNULL)     COLORBLUE;
NOTEXT_BS状态_6 :  IF(BS_STATUS=6,  BS_STATUS,  DRAWNULL)     COLORCYAN;



