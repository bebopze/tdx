板块  :=  IFC(TOOLS_RPS.板块,  1,  TESTSKIP(1))     NODRAW;





{----------------------------------------------------------------------}
MA5  := MA(C,5)    COLORWHITE;
MA10 := MA(C,10)   COLORYELLOW;
MA20 := MA(C,20)   COLORMAGENTA;
MA50 := IF(MA(C, 50)=DRAWNULL,  0,  MA(C, 50))   COLORGREEN ;
MA100:= IF(MA(C,100)=DRAWNULL,  0,  MA(C,100))   COLORLIGRAY;
MA200:= IF(MA(C,200)=DRAWNULL,  0,  MA(C,200))   COLORBLUE  ;
MA250:= IF(MA(C,250)=DRAWNULL,  0,  MA(C,250));










{ -------------------------------------------------------------- 第1版（2024-4-17）}
MA50极值  :=  MA50占比.上MA50占比% < 25   NODRAW;
新低极值  :=  新高_新低.差值 < -150       NODRAW;

MA50高位  :=  MA50占比.上MA50占比% >65    NODRAW;

新高_正   :=  新高_新低.差值 > 0          NODRAW;
新高_负   :=  新高_新低.差值 < 0          NODRAW;



放量      :   AMO>MA(AMO,5)  ||  AMO>MA(AMO,10)    NODRAW;







MA微多头 :=  C>MA5 AND MA5>MA10                 AND     MA5>REF(MA5,1) AND MA10>REF(MA10,1)                           NODRAW;

MA多头   :=  C>MA5 AND MA5>MA10 AND MA10>MA20   AND     MA5>REF(MA5,1) AND MA10>REF(MA10,1)  AND MA20>REF(MA20,1)     NODRAW;
MA大多头 :=  MA多头             AND MA20>MA50   AND     MA50>REF(MA50,1)                                              NODRAW;



MA转空1  :=  C<MA5 AND C<MA10                   AND   ( MA5<REF(MA5,1) AND MA10<REF(MA10,1)                       )   NODRAW;
MA转空2  :=  C<MA5 AND C<MA10 AND C<MA20        AND   (                    MA10<REF(MA10,1)  ||  MA20<REF(MA20,1) )   NODRAW;

MA高位转空 :=  ( 中期涨幅N(20)>=35 AND 中期天数N(20)>=20 )     AND     ( C<MA5 AND C<MA10 )     AND     ( MA5<REF(MA5,1) ||  MA10<REF(MA10,1) )     AND     ( C/HHV(C,10)<=0.93 )     NODRAW;

MA转空   :=  MA转空1 || MA转空2 || MA高位转空   NODRAW;





MA空头   :=  C<MA5 AND C<MA10 AND C<MA20        AND   ( MA5<REF(MA5,1) AND MA10<REF(MA10,1)                       )   NODRAW;
MA大空头 :=  C<MA5 AND C<MA10 AND C<MA20        AND   ( MA5<REF(MA5,1) AND MA10<REF(MA10,1)  AND MA20<REF(MA20,1) )   NODRAW;


涨幅  :=  C/REF(C,1)*100-100;
C振幅 :=  C/L*100-100;



大盘爆量     :  "880003$AMO">=10000* 100000000   NODRAW;  { 大盘-万亿 }
大盘爆量大阳 :  大盘爆量                         AND   (涨幅=HHV(涨幅,10)  ||  涨幅>=1.5  ||  (C振幅>=1.1 AND 涨幅>=0.8)   )   NODRAW;



中期ZF_左侧试仓 :=  IF(TOOLS_RPS.A股,  30,   IF(TOOLS_RPS.板块,  25,   IF(TOOLS_RPS.大盘,  20,  -1)));
中期ZF_左侧买   :=  IF(TOOLS_RPS.A股,  40,   IF(TOOLS_RPS.板块,  30,   IF(TOOLS_RPS.大盘,  25,  -1)));
中期ZF_右侧买   :=  IF(TOOLS_RPS.A股,  50,   IF(TOOLS_RPS.板块,  35,   IF(TOOLS_RPS.大盘,  30,  -1)));





中期调整极值 := 中期调整幅度 < -15;


左侧试仓1 :  放量   AND   中期涨幅<中期ZF_左侧试仓       AND
            (   
                ( COUNT(MA50极值, 10)>=5   AND   COUNT(新低极值, 5)>=1   AND   COUNT(涨幅>=3.5 || (C振幅>=3 AND 涨幅>=2.5), 3)   AND   C>=MA5   )       { 股灾   - 买点 }
            ||  ( COUNT(MA50极值, 10)>=3   AND   MA50>=REF(MA50,1)       AND   COUNT(涨幅>=3.5 || (C振幅>=3 AND 涨幅>=2.5), 3)   AND   C>=MA5   )       { 小股灾 - 买点 }

            )   COLORWHITE NODRAW;



左侧试仓2 :  放量   AND   中期涨幅<中期ZF_左侧试仓       AND
            (   
                ( COUNT(中期调整极值, 10)  AND   MA50>=REF(MA50,1)       AND   COUNT(涨幅>=3.5 || (C振幅>=3 AND 涨幅>=2.5), 3)   AND   C>=MA5   )       { 震荡   - 买点 }

            )   COLORWHITE NODRAW;



左侧试仓 :  左侧试仓1 || 左侧试仓2           COLORWHITE NODRAW;









左侧买1  :=    COUNT(左侧试仓1, 3)     AND     放量   AND   中期涨幅 < 中期ZF_左侧买   AND   MA多(5)                                                                       COLORGREEN NODRAW;   { 股灾 - 买点 }

左侧买2  :=    MA多(50)                AND     放量   AND   中期涨幅 < 中期ZF_左侧买   AND   COUNT(左侧试仓2, 5)   AND   COUNT(涨幅>=3.5 || (C振幅>=3 AND 涨幅>=2.5), 3)   COLORGREEN NODRAW;   { 震荡 - 买点 }

左侧买0 :=  左侧买1 || 左侧买2     COLORGREEN NODRAW;







右侧买1  :  COUNT(左侧买1, 10)     AND     新高_正   AND   放量                    AND   (   MA微多头 || MA多头 ||  C>=MA50   )                 COLORMAGENTA NODRAW;   { 股灾 - 买点 }

右侧买2  :  COUNT(左侧买2,  5)     AND     1         AND   放量                    AND   (   MA多(5) AND MA多(10) AND MA多(20) AND MA多(50)  )  COLORMAGENTA NODRAW;   { 震荡 - 买点 }
           
右侧买3  :  COUNT(左侧买2,  5)     AND     1         AND   放量                    AND   (               MA多头 AND C>=MA50   )                 COLORMAGENTA NODRAW;   { 震荡 - 买点 }



上半年线 := C>MA100 || C>MA200;

右侧买4  :        (周多      AND   中期涨幅 < 中期ZF_右侧买   AND   MA多头   AND   上半年线)
	       ||     (月多      AND   中期涨幅 < 中期ZF_右侧买   AND   MA多头)
           ||     (均线萌出  AND   中期涨幅 < 中期ZF_右侧买               )

		   ||     (MA大多头  AND   中期涨幅 < 中期ZF_右侧买   AND   放量  )   COLORMAGENTA NODRAW;




{右侧买5 :=   COUNT(左侧买1, 25)   AND   COUNT(右侧买1, 15)   AND   MA多头;    { 有待商榷 ... }


右侧买0 :  右侧买1  ||  右侧买2  ||  右侧买3  ||  右侧买4     COLORMAGENTA NODRAW;










大盘MA50高位   :=  MA50占比.上MA50占比%>=65;
大盘MA50大高位 :=  MA50占比.上MA50占比%>=80;

个股中期高位   :=  中期涨幅N(20)>=35 AND 中期天数N(20)>=20;

高位 : 大盘MA50高位 || 个股中期高位     COLORRED NODRAW;





强势卖出1 := COUNT(高位  ,   5)           AND   MA多头     AND   中期涨幅N(20)>=35                           COLORRED NODRAW;       { 股灾   - （反弹）到顶 }
强势卖出2 :=                                    MA多头     AND   中期涨幅N(20)>=35 AND 中期天数N(20)>=20     COLORRED NODRAW;       { 震荡   -         到顶 }
强势卖出3 := COUNT(大盘MA50大高位,   5)   AND   MA大多头   AND   中期涨幅N(20)>=40                           COLORRED NODRAW;       { 牛市   - （中期）到顶 }

强势卖出  :   强势卖出1  ||  强势卖出2  ||  强势卖出3     COLORRED NODRAW;










左侧卖1 :=  COUNT(高位,  10)             AND     COUNT(MA多头, 20)   AND   MA转空     COLORBLUE NODRAW;       { 股灾   - （反弹）到顶  转空 }
左侧卖2 :=  MA空头                                                                    COLORBLUE NODRAW;       { 震荡 -                 转空 }

左侧卖  :   左侧卖1  ||  左侧卖2     COLORBLUE NODRAW;





右侧卖1 :=  COUNT(高位,  15)   AND   
           (
		        (  COUNT(MA多头, 25)   AND   MA空(20)  )
	       ||   (  COUNT(MA多头, 30)   AND   MA空头    )
           );                                                          { 股灾 - 到顶 转大空头 }


右侧卖2 :=    MA大空头;                                                { 震荡 -        大空头 }


右侧卖  :   右侧卖1  ||  右侧卖2     COLORCYAN NODRAW;







{ --------------------------- （趋势股）二波 }

趋势股二波  :=   RPS一线红(99)   AND   COUNT(右侧买0, 20)   AND   COUNT(左侧卖||右侧卖, 10)     AND     EVERY(MA多(50) AND MA多(100) AND MA多(200)  AND  MA20>MA50,   15)   AND   MA50>MA100 AND MA50>MA200     COLORGREEN NODRAW;

左侧买3     :=   趋势股二波     AND     放量   AND   (MA微多头 AND 上MA(20))   COLORGREEN NODRAW;       { 二波 - 买点 } 

左侧买      :   左侧买0 || 左侧买3     COLORGREEN NODRAW;



{ --------------------------- （强势股）二波 }

强势股二波  :=  RPS一线红(99)   AND   COUNT(强势卖出, 15)   AND   COUNT(左侧卖||右侧卖, 10)     AND     EVERY(MA多(20) AND MA多(50) AND MA20>MA50,   15)     COLORMAGENTA NODRAW;

右侧买5     :=   强势股二波     AND     放量   AND   (均线萌出||月多)   COLORMAGENTA NODRAW;

右侧买      :  右侧买0 || 右侧买5     COLORMAGENTA NODRAW;













BS_STATUS_0  :  IF(右侧卖, 6,   IF(左侧卖, 5,   IF(强势卖出, 4,   IF(右侧买, 3,   IF(左侧买, 2,   IF(左侧试仓, 1,   0))))))     COLORWHITE   NODRAW;
BS_STATUS_1  :  IF(BS_STATUS_0>0,   BS_STATUS_0,   REF(BS_STATUS_0,  BARSLAST(BS_STATUS_0 > 0))   )                             COLORMAGENTA NODRAW;

BS_STATUS    :  IF(COUNT(BS_STATUS_1=6, 3)  AND  BS_STATUS_1=5,   6,
                IF(REF(BS_STATUS_1,1)=3     AND  BS_STATUS_1<3,   3,

                BS_STATUS_1))     COLORWHITE;





NOTEXT_BS状态_1 :  IF(BS_STATUS=1,  BS_STATUS,  DRAWNULL)     COLORWHITE;
NOTEXT_BS状态_2 :  IF(BS_STATUS=2,  BS_STATUS,  DRAWNULL)     COLORGREEN;
NOTEXT_BS状态_3 :  IF(BS_STATUS=3,  BS_STATUS,  DRAWNULL)     COLORMAGENTA;
NOTEXT_BS状态_4 :  IF(BS_STATUS=4,  BS_STATUS,  DRAWNULL)     COLORRED;
NOTEXT_BS状态_5 :  IF(BS_STATUS=5,  BS_STATUS,  DRAWNULL)     COLORBLUE;
NOTEXT_BS状态_6 :  IF(BS_STATUS=6,  BS_STATUS,  DRAWNULL)     COLORCYAN;





