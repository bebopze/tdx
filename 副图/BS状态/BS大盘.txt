{  TESTSKIP(  NOT(TOOLS_RPS.大盘)  );  }





{----------------------------------------------------------------------}
MA5  := MA(C, 5)   COLORWHITE;
MA10 := MA(C,10)   COLORYELLOW;
MA20 := MA(C,20)   COLORMAGENTA;
MA50 := IF(MA(C, 50)=DRAWNULL,  0,  MA(C, 50))   COLORGREEN ;
MA100:= IF(MA(C,100)=DRAWNULL,  0,  MA(C,100))   COLORLIGRAY;
MA200:= IF(MA(C,200)=DRAWNULL,  0,  MA(C,200))   COLORBLUE  ;
MA250:= IF(MA(C,250)=DRAWNULL,  0,  MA(C,250));



A股  :=  TOOLS_RPS.A股;
板块 :=  TOOLS_RPS.板块;
大盘 :=  TOOLS_RPS.大盘;







{ ---------------------------------------------------------------------- }
中期涨幅 :   中期涨幅.中期涨幅      NODRAW;


一二阶段 :=   CON_一二阶段.一二阶段 NODRAW;
四阶段   :=   四阶段.四阶段         NODRAW;



SSF1     :=  SSF1.SSF1              NODRAW;
上SSF    :=  上SSF.上SSF            NODRAW;
下SSF    :=  下SSF.下SSF            NODRAW;

SSF多    :=  SSF多.SSF多            NODRAW;
SSF空    :=  SSF空.SSF空            NODRAW;



MA50占比      :=   大盘量化.MA50占比     NODRAW;
月多占比      :=   大盘量化.月多占比     NODRAW;
新高新低_差值 :=   新高_新低.差值        NODRAW;







{ -------------------------------------------------------------- 第1版（2024-4-17）}

MA50极值_VAL  :  IF(一二阶段,   25,   IF(四阶段,   15,   20))   NODRAW;
MA50高位_VAL  :  IF(一二阶段,   75,   IF(四阶段,   60,   65))   NODRAW;



MA50极冰  :  MA50占比 <= 5                      NODRAW;



MA50极值  :  MA50占比 < MA50极值_VAL            NODRAW;
新低极值  :  新高新低_差值 < -150               NODRAW;

MA50高位  :  MA50占比 > MA50高位_VAL            NODRAW;

中继下跌  :  COUNT(MA50占比 <50,   50) >= 45    NODRAW;


新高_正   :=  新高新低_差值 > 0          NODRAW;
新高_负   :=  新高新低_差值 < 0          NODRAW;



放量      :   AMO>MA(AMO,5)  ||  AMO>MA(AMO,10)    NODRAW;







{ ----------------------------------------------------- 多↑ }

上MA_NUM   := 上MA(5)   + 上MA(10)   + 上MA(20);
MA向上_NUM := MA向上(5) + MA向上(10) + MA向上(20);
MA多_NUM   := MA多(5)   + MA多(10)   + MA多(20);

MA多_NUM2  := MA多(5)   + MA多(10)   + MA多(20)   + MA多(50)   + MA多(100)   + MA多(200);





MA微多头 :=  C>MA5 AND MA5>MA10                 AND     MA5>REF(MA5,1) AND MA10>REF(MA10,1)                           NODRAW;

MA多头   :=  C>MA5 AND MA5>MA10 AND MA10>MA20   AND     MA5>REF(MA5,1) AND MA10>REF(MA10,1)  AND MA20>REF(MA20,1)     NODRAW;
MA大多头 :=  MA多头             AND MA20>MA50   AND     MA50>REF(MA50,1)                                              NODRAW;





{ ----------------------------------------------------- 空↓ }

下MA_NUM   := 下MA(5)   + 下MA(10)   + 下MA(20);
MA向下_NUM := MA向下(5) + MA向下(10) + MA向下(20);
MA空_NUM   := MA空(5)   + MA空(10)   + MA空(20);





MA转空1    :=  C<MA5 AND C<MA10                   AND   ( MA向下_NUM >=2            )                           NODRAW;
MA转空2    :=  C<MA5 AND C<MA10 AND C<MA20        AND   ( MA向下(10)  || MA向下(20) )                           NODRAW;
MA转空3    :=  下SSF                              AND   ( MA空_NUM>=1  ||  MA向下_NUM>=2  ||  下MA_NUM>=3 )     NODRAW;

MA高位转空 :=  ( MA50高位 )     AND     ( 下MA(5) AND 下MA(10) )     AND     ( MA向下(5) ||  MA向下(10) )     AND     ( C/HHV(C,10)<=0.93 )     NODRAW;

MA转空     :=  MA转空1 || MA转空2 || MA转空3 || MA高位转空   NODRAW;





MA空头1  :=   C<MA5 AND C<MA10 AND C<MA20        AND                                                           (                    MA向下_NUM >= 2  )     COLORBLUE NODRAW;   {  ALL场景 - 均线 空头  }
MA空头2  :=   MA转空                             AND   COUNT({MA微多头 ||} MA多头 || MA大多头,   10)     AND   (   下SSF   AND      下MA(20)         )     COLORBLUE NODRAW;   {  震荡市  - 均线 空头  }
MA空头   :=   MA空头1   ||   MA空头2     COLORBLUE NODRAW;





MA大空头1 :=  C<MA5 AND C<MA10 AND C<MA20       AND   ( MA向下(20)   AND   MA向下_NUM >= 2  )                    COLORCYAN NODRAW;
MA大空头2 :=  MA空头                            AND   ( SSF空        AND   MA空(20)   AND   SSF1<MA20      )     COLORCYAN NODRAW;
MA大空头3 :=  MA转空                            AND   ( COUNT(MA向上(50),   5)        AND   EVERY(MA向下(50),   2)     )     COLORCYAN NODRAW;
MA大空头  :=  MA大空头1   ||   MA大空头2   ||   MA大空头3       COLORCYAN NODRAW;







{ ---------------------------------------------------------------------------------------------------------- }
涨幅       :=  C/REF(C,1)  *100-100   NODRAW;
涨幅_MA10  :=  ABS(MA(涨幅,  10))     NODRAW;



左侧试仓B :=  上SSF   ||    新高_正   ||   涨幅/涨幅_MA10 >= 2   ||   ( MA多_NUM>=1  ||  MA向上_NUM>=2  ||  上MA_NUM>=3 );

左侧B     :=  上SSF   ||    新高_正;
右侧B     :=  SSF多   AND   新高_正;



{ ---------------------------------------------------------------------------------------------------------- }
{涨幅  :=  C/REF(C,1)*100-100;}
C振幅 :=  C/L*100-100;



{大盘爆量    :   "880003$AMO" >= 10000* 100000000   NODRAW;  { 大盘-万亿 }
大盘爆量     :=  大盘爆量.大盘爆量                  NODRAW;
大盘爆量大阳 :   大盘爆量                           AND   (涨幅=HHV(涨幅,10)  ||  涨幅>=1.5  ||  (C振幅>=1.1 AND 涨幅>=0.8)   )   NODRAW;



中期ZF_左侧试仓 :=  IF(A股,  30,   IF(板块,  25,   IF(大盘,  20,  -1)));
中期ZF_左侧买   :=  IF(A股,  40,   IF(板块,  30,   IF(大盘,  25,  -1)));
中期ZF_右侧买   :=  IF(A股,  50,   IF(板块,  35,   IF(大盘,  30,  -1)));





{ ---------------------------------------------------------------------------------------------------------- BS状态 }

左侧试仓 :  左侧试仓B   AND   放量   AND   中期涨幅<中期ZF_左侧试仓       AND
            (
                ( COUNT(MA50极冰, 10)>=1   AND   COUNT(新低极值, 5)>=1   AND   COUNT(涨幅>=1.5 || (C振幅>=1.3 AND 涨幅>=0.8), 3)   AND   C>=MA5   )          { 极度冰点 }

            ||  ( COUNT(MA50极值, 10)>=5   AND   COUNT(新低极值, 5)>=1   AND   COUNT(涨幅>=1.5 || (C振幅>=1.3 AND 涨幅>=0.8), 3)   AND   C>=MA5   )          { 冰点     }

            ||  ( COUNT(MA50极值, 10)>=3   AND   MA50>=REF(MA50,1)       AND   COUNT(涨幅>=1.5 || (C振幅>=1.3 AND 涨幅>=0.8), 3)   AND   C>=MA5   )          { 震荡     }



            ||  ( 中继下跌                 AND   COUNT(新低极值, 5)>=1   AND   COUNT(涨幅>=1.5 || (C振幅>=1.3 AND 涨幅>=0.8), 3)   AND   C>=MA5   )          { 中继下跌 }

            ) COLORWHITE   NODRAW;





左侧买1  :=    左侧B   AND     COUNT(左侧试仓, 3)     AND     放量   AND   中期涨幅 < 中期ZF_左侧买   AND   MA多(5)                                                     COLORGREEN NODRAW;     { 股灾 - 买点 }
左侧买2  :=    左侧B   AND     MA多(50)               AND     放量   AND   中期涨幅 < 中期ZF_左侧买   AND   大盘爆量大阳   AND   新高_正  AND  COUNT(新高_负,3)         COLORGREEN NODRAW;     { 震荡 - 买点 }
左侧买3  :=    左侧B   AND     MA多(50)  AND MA微多头 AND     放量   AND   中期涨幅 < 中期ZF_左侧买                        AND   新高_正                                COLORGREEN NODRAW;     { 震荡 - 买点 }

左侧买4  :=    左侧B   AND     MA多_NUM2>=5           AND     放量   AND   中期涨幅 < 中期ZF_左侧买                        AND   新高_正  AND  (大均线多头 || 月多)     COLORGREEN NODRAW;     { 牛市（暴跌 - 大反弹） - 买点 }


左侧买 :  左侧买1 || 左侧买2 || 左侧买3 || 左侧买4          COLORGREEN   NODRAW;







右侧买1  :  右侧B   AND     COUNT(左侧买1, 15)   AND   COUNT(新高_正,10)>=6   AND   新高_正   AND   放量                    AND   (   MA微多头 || MA多头 ||  C>=MA50   )  COLORMAGENTA NODRAW;       { 股灾 - 买点 }
右侧买2  :  右侧B   AND     COUNT(左侧买2, 15)   AND                                新高_正   AND   放量   AND   大盘爆量   AND   (               MA多头 AND C>=MA50   )  COLORMAGENTA NODRAW;       { 震荡 - 买点 }


右侧买3  :  右侧B   AND     COUNT(左侧买1, 10)   AND   MA多头   {AND   放量   AND   新高_正}     COLORMAGENTA NODRAW;     {  股灾 ->  小均线 多头     -   有待观察 2024-07-22  }



上半年线 := C>MA100 || C>MA200;

右侧买4  :  右侧B   AND     新高_正   AND
            (
                    (周多        AND   中期涨幅 < 中期ZF_右侧买   AND   MA多头   AND   上半年线)
	         ||     (月多        AND   中期涨幅 < 中期ZF_右侧买   AND   MA多头)
             ||     (均线预萌出  AND   中期涨幅 < 中期ZF_右侧买               )

		     ||     (MA大多头    AND   中期涨幅 < 中期ZF_右侧买   AND   放量  )
           )        COLORMAGENTA NODRAW;





一二阶段_右侧买 :=  一二阶段 AND 1     COLORMAGENTA NODRAW;   { 大牛市 - 年 级别    TODO }





右侧买 : 右侧买1  ||  右侧买2  ||  右侧买3  ||  右侧买4     COLORMAGENTA NODRAW;











{ --------------------------------------------- 4 }
大盘MA50高位 :=  MA50占比 >= 65;


强势卖出1  :   大盘MA50高位   AND   MA多头     AND   COUNT(新高_正,15)>=12     COLORRED NODRAW;
强势卖出2  :   MA50占比>=95   AND   月多占比>=50                               COLORRED NODRAW;

强势卖出   :   强势卖出1   ||   强势卖出2     COLORRED NODRAW;





{ --------------------------------------------- 5 }
左侧卖1 :=    COUNT(大盘MA50高位,  10)     AND     COUNT(MA多头, 20)   AND   MA转空     COLORBLUE NODRAW;      { 中期涨幅 - 到顶 转空 }
左侧卖2 :=    MA空头;                                                                                          { 震荡市   -      转空 }

左侧卖  :     左侧卖1  ||  左侧卖2     COLORBLUE NODRAW;





右侧卖1 :=  COUNT(大盘MA50高位,  15)   AND
           (
		        (  COUNT(MA多头, 20)   AND   MA空(20)  )
	       ||   (  COUNT(MA多头, 30)   AND   MA空头    )
           );                                                          { 中期涨幅 - 到顶 转大空头 }


右侧卖2 :=    MA大空头;                                                { 震荡市   -        大空头 }


右侧卖  :  右侧卖1  ||  右侧卖2     COLORCYAN NODRAW;










BS状态_0  :  IF(右侧卖, 6,     IF(左侧卖, 5,     IF(强势卖出, 4,     IF(右侧买, 3,     IF(左侧买, 2,     IF(左侧试仓, 1,   0))))))       COLORWHITE   NODRAW;
BS状态_1  :  IF(BS状态_0>0,    BS状态_0,         REF(BS状态_0,  BARSLAST(BS状态_0 > 0))   )                                              COLORMAGENTA NODRAW;

BS状态    :  IF(COUNT(BS状态_1=6, 3)  AND  BS状态_1=5,   6,
             IF(REF(BS状态_1,1)=3     AND  BS状态_1<3,   3,

             BS状态_1))     COLORWHITE NODRAW;









TESTSKIP(X);
{ -------------------------------------------------------------------------------------------------------------------- }
SSF_MA20多 :=  SSF_MA20多.SSF_MA20多;





BS状态2 :

IF(BS状态=2 AND NOT(上SSF     ),     BS状态-0.5,      { 假 - 左侧B     /     真 - 左侧S }
IF(BS状态=3 AND NOT(SSF_MA20多),     BS状态-0.5,      { 假 - 右侧B     /     真 - 左侧S }
IF(BS状态=4 AND NOT(SSF_MA20多),     BS状态-0.5,      { 假 - 强势S     /     真 - 左侧S }

BS状态)))     COLORWHITE;










{ -------------------------------------------------------------------------------------------------------------------- }
NOTEXT_BS状态_1 :  IF(BS状态=1,                       BS状态    ,   DRAWNULL)     COLORWHITE;


NOTEXT_BS状态2  :  IF(BS状态=2 AND NOT(上SSF)     ,   BS状态-0.5,   DRAWNULL)     COLORLIGREEN   DOTLINE;     { 假-左侧B  /  真-左侧S }
NOTEXT_BS状态_2 :  IF(BS状态=2 AND     上SSF      ,   BS状态    ,   DRAWNULL)     COLORGREEN;


NOTEXT_BS状态3  :  IF(BS状态=3 AND NOT(SSF_MA20多),   BS状态-0.5,   DRAWNULL)     COLORLIMAGENTA DOTLINE;     { 假-右侧B  /  真-左侧S }
NOTEXT_BS状态_3 :  IF(BS状态=3 AND     SSF_MA20多 ,   BS状态    ,   DRAWNULL)     COLORMAGENTA;


NOTEXT_BS状态4  :  IF(BS状态=4 AND NOT(SSF_MA20多),   BS状态-0.5,   DRAWNULL)     COLORLIRED     DOTLINE;     { 假-强势S  /  真-左侧S }
NOTEXT_BS状态_4 :  IF(BS状态=4 AND     SSF_MA20多 ,   BS状态    ,   DRAWNULL)     COLORRED;


NOTEXT_BS状态_5 :  IF(BS状态=5,  BS状态,  DRAWNULL)     COLORBLUE;
NOTEXT_BS状态_6 :  IF(BS状态=6,  BS状态,  DRAWNULL)     COLORCYAN;


