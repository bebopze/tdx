{   TESTSKIP(  NOT(TOOLS_RPS.个股)  );   }





{----------------------------------------------------------------------}
MA5  := MA(C, 5)   COLORWHITE;
MA10 := MA(C,10)   COLORYELLOW;
MA20 := MA(C,20)   COLORMAGENTA;
MA50 := IF(MA(C, 50)=DRAWNULL,  0,  MA(C, 50))   COLORGREEN ;
MA100:= IF(MA(C,100)=DRAWNULL,  0,  MA(C,100))   COLORLIGRAY;
MA200:= IF(MA(C,200)=DRAWNULL,  0,  MA(C,200))   COLORBLUE  ;
MA250:= IF(MA(C,250)=DRAWNULL,  0,  MA(C,250));





月多       :=  TOOLS.月多;
周多       :=  TOOLS.周多;

均线预萌出 :=  均线预萌出.均线预萌出;
均线萌出   :=  均线萌出.均线萌出;
大均线多头 :=  大均线多头.大均线多头;



上影大阴   :=  上影大阴.上影大阴;



中期涨幅   :=  中期涨幅.中期涨幅;


中期涨幅_20 := 中期涨幅N(20);
中期天数_20 := 中期天数N(20);



SSF空       := SSF空.SSF空;
下SSF       := 下SSF.下SSF;





MA50占比      :=  大盘量化.MA50占比;
新高新低_差值 :=  新高_新低.差值;






{ -------------------------------------------------------------- 第1版（2024-4-17）}
MA50极值  :=  MA50占比 < 25              NODRAW;
新低极值  :=  新高新低_差值 < -150       NODRAW;

MA50高位  :=  MA50占比 >65               NODRAW;

新高_正   :=  新高新低_差值 > 0          NODRAW;
新高_负   :=  新高新低_差值 < 0          NODRAW;



放量      :   AMO>MA(AMO,5)  ||  AMO>MA(AMO,10)    NODRAW;







{ ----------------------------------------------------- 多↑ }

MA微多头 :=  C>MA5 AND MA5>MA10                 AND     MA5>REF(MA5,1) AND MA10>REF(MA10,1)                           NODRAW;

MA预多头 :=  C>MA5 AND MA5>MA10 AND MA5 >MA20   AND     MA5>REF(MA5,1) AND MA10>REF(MA10,1)  AND MA20>REF(MA20,1)     NODRAW;
MA多头   :=  C>MA5 AND MA5>MA10 AND MA10>MA20   AND     MA5>REF(MA5,1) AND MA10>REF(MA10,1)  AND MA20>REF(MA20,1)     NODRAW;

MA大多头 :=  MA多头             AND MA20>MA50   AND     MA50>REF(MA50,1)                                              NODRAW;





{ ----------------------------------------------------- 空↓ }

下MA_NUM   := 下MA(5)   + 下MA(10)   + 下MA(20);
MA向下_NUM := MA向下(5) + MA向下(10) + MA向下(20);
MA空_NUM   := MA空(5)   + MA空(10)   + MA空(20);



MA转空1    :=  C<MA5 AND C<MA10                   AND   ( MA向下_NUM >=2            )                           NODRAW;
MA转空2    :=  C<MA5 AND C<MA10 AND C<MA20        AND   ( MA向下(10)  || MA向下(20) )                           NODRAW;
MA转空3    :=  下SSF                              AND   ( MA空_NUM>=1  ||  MA向下_NUM>=2  ||  下MA_NUM>=3 )     NODRAW;

MA高位转空 :=  ( 中期涨幅_20>=80 AND 中期天数_20>=20 )     AND     ( 下MA(5) AND 下MA(10) )     AND     ( MA向下(5) ||  MA向下(10) )     AND     ( C/HHV(C,10)<=0.93 )     NODRAW;

MA转空     :  MA转空1 || MA转空2 || MA转空3 || MA高位转空   NODRAW;





MA空头1   :=  C<MA5 AND C<MA10 AND C<MA20         AND   (                  MA向下_NUM>=2                  )     NODRAW;
MA空头2   :   MA转空   AND   SSF空                AND   ( MA空_NUM>=2  ||  MA向下_NUM>=3  ||  下MA_NUM>=3 )     NODRAW;
MA空头3   :   MA转空   AND   MA空(20)             AND   ( 下SSF                                           )     NODRAW;

MA空头    :  MA空头1  ||  MA空头2  ||  MA空头3     NODRAW;



MA大空头  :=  C<MA5 AND C<MA10 AND C<MA20         AND   ( MA向下(20)   AND   MA向下_NUM>=2  )     NODRAW;



MA大空头2 :=  C<MA5 AND C<MA10 AND C<MA20 AND C<MA50     AND     MA5<REF(MA5,1) AND MA10<REF(MA10,1)  AND  ( MA20<REF(MA20,1) || MA50<REF(MA50,1) || MA20<MA50 )     NODRAW;







{ ---------------------------------------------------------------------------------------------------------- }
涨幅  :=  C/REF(C,1)  *100-100;
C振幅 :=  C/L         *100-100;



{大盘爆量    :  "880003$AMO">=10000* 100000000   NODRAW;  { 大盘-万亿 }
大盘爆量     := 大盘爆量.大盘爆量                NODRAW;
大盘爆量大阳 :  大盘爆量                         AND   (涨幅=HHV(涨幅,10)  ||  涨幅>=1.5  ||  (C振幅>=1.1 AND 涨幅>=0.8)   ) NODRAW;



中期ZF_左侧试仓 :=  IF(TOOLS_RPS.A股,  30,   IF(TOOLS_RPS.板块,  25,   IF(TOOLS_RPS.大盘,  20,  -1)));
中期ZF_左侧买   :=  IF(TOOLS_RPS.A股,  40,   IF(TOOLS_RPS.板块,  30,   IF(TOOLS_RPS.大盘,  25,  -1)));
中期ZF_右侧买   :=  IF(TOOLS_RPS.A股,  50,   IF(TOOLS_RPS.板块,  35,   IF(TOOLS_RPS.大盘,  30,  -1)));





中期调整极值 := 中期调整幅度 < -15;


左侧试仓1 :=  放量   AND   中期涨幅<中期ZF_左侧试仓       AND
            (
                ( COUNT(MA50极值, 10)>=5   AND   COUNT(新低极值, 5)>=1   AND   COUNT(涨幅>=3.5 || (C振幅>=3 AND 涨幅>=2.5), 3)   AND   C>=MA5   )       { 股灾   - 买点 }
            ||  ( COUNT(MA50极值, 10)>=3   AND   MA50>=REF(MA50,1)       AND   COUNT(涨幅>=3.5 || (C振幅>=3 AND 涨幅>=2.5), 3)   AND   C>=MA5   )       { 小股灾 - 买点 }

            )   COLORWHITE NODRAW;



左侧试仓2 :=  放量   AND   中期涨幅<中期ZF_左侧试仓       AND
            (
                ( COUNT(中期调整极值, 10)  AND   MA50>=REF(MA50,1)       AND   COUNT(涨幅>=3.5 || (C振幅>=3 AND 涨幅>=2.5), 3)   AND   C>=MA5   )       { 震荡   - 买点 }

            )   COLORWHITE NODRAW;



左侧试仓 :  左侧试仓1 || 左侧试仓2           COLORWHITE NODRAW;









左侧买1  :=    COUNT(左侧试仓1, 3)     AND     放量   AND   中期涨幅 < 中期ZF_左侧买   AND   MA多(5)                                                                       COLORGREEN NODRAW;   { 股灾 - 买点 }

左侧买2  :=    MA多(50)                AND     放量   AND   中期涨幅 < 中期ZF_左侧买   AND   COUNT(左侧试仓2, 5)   AND   COUNT(涨幅>=3.5 || (C振幅>=3 AND 涨幅>=2.5), 3)   COLORGREEN NODRAW;   { 震荡 - 买点 }



左侧买3  :     月多   AND   (大均线多头 AND MA多(5) AND MA多(20) AND MA多(50)  AND MA5>MA10 AND MA5>MA20 AND MA20>MA50)     COLORGREEN NODRAW;



左侧买0 :=  左侧买1 || 左侧买2 || 左侧买3     COLORGREEN NODRAW;







右侧买1  :=  COUNT(左侧买1, 10)     AND     新高_正   AND   放量                    AND   (   MA微多头 || MA多头 ||  C>=MA50   )                 COLORMAGENTA NODRAW;   { 股灾 - 买点 }

右侧买2  :=  COUNT(左侧买2,  5)     AND     1         AND   放量                    AND   (   MA多(5) AND MA多(10) AND MA多(20) AND MA多(50)  )  COLORMAGENTA NODRAW;   { 震荡 - 买点 }

右侧买3  :=  COUNT(左侧买2,  5)     AND     1         AND   放量                    AND   (               MA多头 AND C>=MA50   )                 COLORMAGENTA NODRAW;   { 震荡 - 买点 }

右侧买4  :  SSF多 AND MA多(20)     COLORMAGENTA NODRAW;   { 趋势 - 买点 }



上半年线 := C>MA100 || C>MA200;

右侧买5  :        (周多      AND   中期涨幅 < 中期ZF_右侧买   AND   MA多头   AND   上半年线)
	       ||     (月多      AND   中期涨幅 < 中期ZF_右侧买   AND   MA多头)
           ||     (均线萌出  AND   中期涨幅 < 中期ZF_右侧买               )

		   ||     (MA大多头  AND   中期涨幅 < 中期ZF_右侧买   AND   放量  )   COLORMAGENTA NODRAW;



右侧买6  :   月多   AND   (均线预萌出 || MA预多头)     COLORMAGENTA NODRAW;


{右侧买7 :=   COUNT(左侧买1, 25)   AND   COUNT(右侧买1, 15)   AND   MA多头;    { 有待商榷 ... }


右侧买0 : 右侧买1  ||  右侧买2  ||  右侧买3  ||  右侧买4  ||  右侧买5  ||  右侧买6     COLORMAGENTA NODRAW;










大盘MA50高位   :=  MA50占比 >= 65;
大盘MA50大高位 :=  MA50占比 >= 80;

个股中期高位   :=  中期涨幅N(20)>=35 AND 中期天数N(20)>=20;

高位 : 大盘MA50高位 || 个股中期高位   NODRAW;





强势卖出1 :=  COUNT(高位  ,   5)           AND   MA多头     AND   中期涨幅>=75                         COLORRED NODRAW;       { 股灾   - （反弹）到顶 }
强势卖出2 :=                                     MA多头     AND   中期涨幅>=75 AND 中期天数_20>=20     COLORRED NODRAW;       { 震荡   -         到顶 }
强势卖出3 :=  COUNT(大盘MA50大高位,   5)   AND   MA大多头   AND   中期涨幅>=75                         COLORRED NODRAW;       { 牛市   - （中期）到顶 }

强势卖出4 :=                                     MA大多头   AND   中期涨幅>=85                         COLORRED NODRAW;       { 动量股 - （中期）到顶 }

强势卖出5 :=  COUNT(上影大阴 AND 中期涨幅_20>=65,   10)   AND   MA大多头   AND   中期涨幅>=75    COLORRED NODRAW;       { 动量股 - （中期）上影/大阴  到顶 }


强势卖出  :   强势卖出1  ||  强势卖出2  ||  强势卖出3  ||  强势卖出4  ||  强势卖出5     COLORRED NODRAW;










左侧卖1 :  COUNT(高位,  10)             AND     COUNT(MA多头, 20)   AND   MA转空     COLORBLUE NODRAW;       { 股灾   - （反弹）到顶  转空 }
左侧卖2 :  MA空头                                                                    COLORBLUE NODRAW;       { 震荡   -               转空 }

左侧卖3 :  COUNT(强势卖出,  10)         AND     MA空_NUM >=1                         COLORBLUE NODRAW;       { 个股高位（倍/危）  -   转空 }



左侧卖  :  左侧卖1  ||  左侧卖2  ||  左侧卖3     COLORBLUE NODRAW;





右侧卖1 :=  COUNT(高位,  15)   AND
           (
		        (  COUNT(MA多头, 25)   AND   MA空(20)  )
	       ||   (  COUNT(MA多头, 30)   AND   MA空头    )
           );                                                                                   { 股灾 - 到顶 转大空头   }


右侧卖2 :=    MA大空头  ||  MA大空头2;                                                          { 震荡 -        大空头   }


右侧卖3 :=    NOT(  一二阶段 || 月多 || RPS一线红(85) || RPS双线红(70) || RPS三线红(60)  );     {  四阶段 - 看一眼都多余 }


右侧卖4 :=    COUNT(左侧卖3,  10)        AND     MA空_NUM >=1 AND 下MA_NUM>=2           COLORBLUE NODRAW;       { 个股高位（倍/危）  -   转大空头 }



右侧卖  :  右侧卖1  ||  右侧卖2  ||  右侧卖3  ||  右侧卖4     COLORCYAN NODRAW;







{ --------------------------- （趋势股）二波 }

趋势股二波  :   RPS一线红(99)   AND   COUNT(右侧买0, 20)   AND   COUNT(左侧卖||右侧卖, 10)     AND     EVERY(MA多(50) AND MA多(100) AND MA多(200)  AND  MA20>MA50,   15)   AND   MA50>MA100 AND MA50>MA200     COLORGREEN NODRAW;

左侧买4     :   趋势股二波     AND     放量   AND   (MA微多头 AND 上MA(20))   COLORGREEN NODRAW;       { 二波 - 买点 }

左侧买      :   左侧买0 || 左侧买4     COLORGREEN NODRAW;



{ --------------------------- （强势股）二波 }
MA20_MA50 : MA20/MA50   COLORMAGENTA NODRAW;

强势股二波  :=  RPS一线红(99)   AND   MA20/MA50<=1.2   AND   COUNT(强势卖出, 15)   AND   COUNT(左侧卖||右侧卖, 10)     AND     EVERY(MA多(20) AND MA多(50) AND MA20>MA50,   15)   COLORMAGENTA NODRAW;

右侧买7     :   强势股二波     AND     放量   AND   (均线萌出||月多)   COLORMAGENTA NODRAW;

右侧买8     :   COUNT(左侧卖,10)<=3     AND     放量   AND   (均线萌出||月多)   COLORMAGENTA NODRAW;


右侧买      :  右侧买0 || 右侧买7 || 右侧买8     COLORMAGENTA NODRAW;













BS状态_0  :  IF(右侧卖, 6,     IF(左侧卖, 5,     IF(强势卖出, 4,     IF(右侧买, 3,     IF(左侧买, 2,     IF(左侧试仓, 1,   0))))))       COLORWHITE   NODRAW;
BS状态_1  :  IF(BS状态_0>0,    BS状态_0,         REF(BS状态_0,  BARSLAST(BS状态_0 > 0))   )                                              COLORMAGENTA NODRAW;

BS状态    :  IF(COUNT(BS状态_1=6, 3)  AND  BS状态_1=5,   6,
             IF(REF(BS状态_1,1)=3     AND  BS状态_1<3,   3,

             BS状态_1))     COLORWHITE NODRAW;









TESTSKIP(X);
{ -------------------------------------------------------------------------------------------------------------------- }
SSF_MA20多 :=  SSF_MA20多.SSF_MA20多;





BS状态2 :

IF(BS状态=2 AND NOT(上SSF     ),     BS状态-0.5,      { 假 - 左侧B     /     真 - 左侧S }
IF(BS状态=3 AND NOT(SSF_MA20多),     BS状态-0.5,      { 假 - 右侧B     /     真 - 左侧S }
IF(BS状态=4 AND NOT(SSF_MA20多),     BS状态-0.5,      { 假 - 强势S     /     真 - 左侧S }

BS状态)))     COLORWHITE;










{ -------------------------------------------------------------------------------------------------------------------- }
NOTEXT_BS状态_1 :  IF(BS状态=1,                       BS状态    ,   DRAWNULL)     COLORWHITE;


NOTEXT_BS状态2  :  IF(BS状态=2 AND NOT(上SSF)     ,   BS状态-0.5,   DRAWNULL)     COLORLIGREEN   DOTLINE;     { 假-左侧B  /  真-左侧S }
NOTEXT_BS状态_2 :  IF(BS状态=2 AND     上SSF      ,   BS状态    ,   DRAWNULL)     COLORGREEN;


NOTEXT_BS状态3  :  IF(BS状态=3 AND NOT(SSF_MA20多),   BS状态-0.5,   DRAWNULL)     COLORLIMAGENTA DOTLINE;     { 假-右侧B  /  真-左侧S }
NOTEXT_BS状态_3 :  IF(BS状态=3 AND     SSF_MA20多 ,   BS状态    ,   DRAWNULL)     COLORMAGENTA;


NOTEXT_BS状态4  :  IF(BS状态=4 AND NOT(SSF_MA20多),   BS状态-0.5,   DRAWNULL)     COLORLIRED     DOTLINE;     { 假-强势S  /  真-左侧S }
NOTEXT_BS状态_4 :  IF(BS状态=4 AND     SSF_MA20多 ,   BS状态    ,   DRAWNULL)     COLORRED;


NOTEXT_BS状态_5 :  IF(BS状态=5,  BS状态,  DRAWNULL)     COLORBLUE;
NOTEXT_BS状态_6 :  IF(BS状态=6,  BS状态,  DRAWNULL)     COLORCYAN;


