N:=3;   {  3-10 }
M:=21;  { 21-60 }
{ ----------------------------------------- }
X_1 :=  ABS(C - REF(C,11));
X_2 :=  SUM(ABS(C-REF(C,1)),  11);
X_3 :=  ABS(X_1 / X_2);
X_4 :=  X_3*(2/N - 2/M) + 2/M;
X_5 :=  X_4*X_4;


SSF1 :   EMA(DMA(C,X_5),   2);







TESTSKIP(X);
{ ---------------------------------------------------------------------------------------- }
NOTEXT_A1 :   IF(SSF1>REF(SSF1,1),   SSF1,   DRAWNULL)     POINTDOT  COLORRED    LINETHICK4;
NOTEXT_A2 :   IF(SSF1>REF(SSF1,1),   SSF1,   DRAWNULL)               COLORRED;

NOTEXT_A3 :   IF(SSF1=REF(SSF1,1),   SSF1,   DRAWNULL)     POINTDOT  COLORWHITE  LINETHICK4;
NOTEXT_A4 :   IF(SSF1=REF(SSF1,1),   SSF1,   DRAWNULL)               COLORWHITE;

NOTEXT_A5 :   IF(SSF1<REF(SSF1,1),   SSF1,   DRAWNULL)     POINTDOT  COLORGREEN  LINETHICK4;
NOTEXT_A6 :   IF(SSF1<REF(SSF1,1),   SSF1,   DRAWNULL)               COLORGREEN;



IF(C<SSF1   AND   SSF1>=REF(SSF1,1),   SSF1,   DRAWNULL)               COLORFF0080;













{ ------------------------------------------- }
中期涨幅   :   中期涨幅N(20)     COLORMAGENTA NODRAW;
中期天数   :   中期天数N(20)     COLORMAGENTA NODRAW;

中期涨幅_H :=  HHV(中期涨幅, 10);


HH :   HH     COLORYELLOW NODRAW;
LL :   LL     COLORCYAN   NODRAW;



{ ------------------------------------------- }
AMO_BL     :   AMO_BL      COLORRED NODRAW;
AMO_BL2    :   AMO_BL2     COLORRED NODRAW;



大盘     :=  INBLOCK('中期信号')   ;   {TOOLS_RPS.大盘}
板块     :=  INBLOCK('板块指数RPS');   {TOOLS_RPS.板块}


距今天数 :=  CURRBARSCOUNT;










{ --------------------------------------------------------------------------------------------------------------------------- }
MA20  :   MA(C,  20)     COLORMAGENTA NODRAW;

MA50  :   MA(C,  50)                                                                                           COLORGREEN  ;
MA60  :   IF(MA50 >REF(MA50, 1)   AND   (COUNT(C<MA50, 10) || REFXV(C<MA50, 10)),   MA(C, 60),   DRAWNULL)     COLORLIGREEN;

MA100 :   MA(C, 100)                                                                                           COLORLIGRAY;
MA120 :   IF(MA100>REF(MA100,1)   AND   (COUNT(C<MA100,10) || REFXV(C<MA100,10)),   MA(C,120),   DRAWNULL)     COLORGRAY;

MA200 :   MA(C, 200)     COLORBLUE;
MA250 :   IF(MA200>REF(MA200,1)   AND   (COUNT(C<MA200,10) || REFXV(C<MA200,10)),   MA(C,250),   DRAWNULL)     COLORCYAN;




NOTEXT_MA20_1  :   IF(SSF1<MA20 AND MA20>REF(MA20,1)     ,MA20,   DRAWNULL)   COLORMAGENTA  LINETHICK1;


NOTEXT_MA50_1  :   IF(C < MA50                ,MA50,   DRAWNULL)   COLORFF0080   LINETHICK1;
NOTEXT_MA50_2  :   IF(MA50  < REF(MA50, 1)    ,MA50,   DRAWNULL)   COLORCYAN     LINETHICK1;

NOTEXT_MA100_1 :   IF(C < MA100               ,MA100,  DRAWNULL)   COLORFF0080   LINETHICK2;
NOTEXT_MA100_2 :   IF(MA100 < REF(MA100,1)    ,MA100,  DRAWNULL)   COLORCYAN     LINETHICK2;

NOTEXT_MA200_1 :   IF(C < MA200               ,MA200,  DRAWNULL)   COLORFF0080   LINETHICK2;
NOTEXT_MA200_2 :   IF(MA200 < REF(MA200,1)    ,MA200,  DRAWNULL)   COLORCYAN     LINETHICK2;





DRAWTEXT_FIX({ISLASTBAR AND} Y,0.01,0.05,0,STRCAT6(LEVEL1HYBLOCK, '   ', MOREHYBLOCK, '     ', CON2STR(FINANCE(8),0), '')),COLORYELLOW;
DRAWTEXT_FIX({ISLASTBAR AND} Y,0.01,0.10,0,GNBLOCK),COLORYELLOW;
DRAWTEXT_FIX({ISLASTBAR AND} Y,0.01,0.15,0,MAINBUSINESS),COLORWHITE;{
DRAWTEXT_FIX( ISLASTBAR AND  Y,0.01,0.25,0,ZDBLOCK),COLORWHITE ;}










{ ----------------------------------------------------------------------------------------------- }
中期调整_N :=  IF( (MA多(100) {|| MA多(200)})  AND  PERIOD>=5{日K},     100,     200);


_H    :=  HHV(H,     中期调整_N);
H_DAY :=  HHVBARS(H, 中期调整_N)  +1;

_L    :=  LLV(L,     H_DAY);
L_DAY :=  LLVBARS(L, H_DAY)  +1;


调整幅度  :   _L/_H *100 -100     COLORCYAN NODRAW;
调整天数  :   H_DAY - L_DAY       COLORCYAN NODRAW;
调整幅度2 :    L/_H *100 -100     COLORCYAN NODRAW;
调整天数2 :   H_DAY -1            COLORCYAN NODRAW;
{ -------------------------------------------------------------- }
中期VOL占比 :   中期VOL占比.中期VOL占比     COLORYELLOW NODRAW;



{ ----------------------------------------------------------------------------------------------- 适用：牛市 }
调整幅度_L1 :          LLV(调整幅度,   MIN(    调整天数2+1 ,  20))                  COLORCYAN NODRAW;
调整幅度_L2 :   REFXV( LLV(调整幅度,   MIN(REF(距今天数,20),  20)),       20)       COLORCYAN NODRAW;

调整幅度_L  :   MIN(调整幅度_L1,   调整幅度_L2)       COLORCYAN NODRAW;



DRAW_调整幅度_L :
DRAWTEXT(BARSSINCEN(上MA(250) AND 调整幅度2=调整幅度_L AND 调整幅度_L<IF(大盘,-5,   IF(板块,-10,   -15)),   2)=0,     IF(L/LL>1.05,   0.99*L,   0.90*L),     VARCAT6(  VAR2STR(调整幅度,0),   '&',   VAR2STR(调整天数,0),  '&','&',   VAR2STR(LLV(中期VOL占比,10),0)  ) )       COLORCYAN;







{ ----------------------------------------------------------------------------------------------- 适用：牛熊市 }
中期涨幅_H1 :   HHV  (中期涨幅   ,   15)              COLORRED NODRAW;
中期涨幅_H2 :   REFXV(中期涨幅_H1,   15)              COLORRED NODRAW;

中期涨幅__H :   MAX(中期涨幅_H1,   中期涨幅_H2)       COLORRED NODRAW;



DRAW_中期涨幅_H :
DRAWTEXT(BARSSINCEN(中期涨幅=中期涨幅__H AND 中期涨幅> IF(大盘,10,   IF(板块,15,   30)),   3)=0,     IF(H/HH>0.95,   0.97*H,   1.06*H),     VARCAT6(  VAR2STR(中期涨幅,0),   '&',   VAR2STR(中期天数,0),   '&','&',   VAR2STR(中期VOL占比,0)  ) )       COLORYELLOW;







{ ----------------------------------------------------------------------------------------------- 适用：熊市 }
_中期调整_N :   BARSLAST(DRAW_中期涨幅_H) + 1     COLORWHITE NODRAW;


__H    :=  HHV(H,     _中期调整_N);
_H_DAY :=  HHVBARS(H, _中期调整_N)  +1;

__L    :=  LLV(L,     _H_DAY);
_L_DAY :=  LLVBARS(L, _H_DAY)  +1;


_调整幅度  :   __L/__H *100 -100     COLORCYAN NODRAW;
_调整天数  :   _H_DAY - _L_DAY       COLORCYAN NODRAW;
_调整幅度2 :     L/__H *100 -100     COLORCYAN NODRAW;
_调整天数2 :   _H_DAY -1             COLORCYAN NODRAW;





_调整幅度_L1 :          LLV(_调整幅度,   MIN(    _调整天数2+1 ,  20))                  COLORWHITE NODRAW;
_调整幅度_L2 :   REFXV( LLV(_调整幅度,   MIN( REF(距今天数,20),  20)),       20)       COLORWHITE NODRAW;


_调整幅度_L  :   MIN(_调整幅度_L1,   _调整幅度_L2)       COLORWHITE NODRAW;



_DRAW_调整幅度L :
DRAWTEXT(BARSSINCEN((下MA(250)||DRAW_调整幅度_L=0) AND _调整幅度2=_调整幅度_L AND _调整幅度_L<IF(大盘,-5,   IF(板块,-10,   -15)),   2)=0,     IF(L/LL>1.05,   0.99*L,   0.90*L),     VARCAT6(  VAR2STR(_调整幅度,0),   '&',   VAR2STR(_调整天数,0),  '&','&',   VAR2STR(LLV(中期VOL占比,10),0)  ) )       COLORCYAN COLORWHITE;







{ ----------------------------------------------------------------------------------------------- 倍危 警戒线 }
CON_倍    :   中期涨幅 > 100                COLORRED NODRAW;
倍_C      :   LLV(L, 中期天数+1)  * 2       COLORRED NODRAW;


NOTEXT_倍 :   IF(CON_倍,   倍_C,   DRAWNULL)       COLORYELLOW;



{ ----------------------------------------------------------------------------------------------- 10倍股 }
N倍 :=  8.5; { 750% }

中期涨幅_H250 :   HH/LL -1       COLORYELLOW NODRAW;
_10倍_C       :   LL * N倍       COLORRED    NODRAW;


NOTEXT__5倍 :=   IF(中期涨幅_H250>=5  AND 上MA(200)   AND   C > _10倍_C/2,       LL *   5,       DRAWNULL)     COLORBLUE;   { 400% }
NOTEXT_10倍 :=   IF(中期涨幅_H250>=N  AND 上MA(200)   AND   C > _10倍_C/2,        _10倍_C,       DRAWNULL)     COLORCYAN;   { 750% };




















{ ----------------------------------------------------------------------------------------------- }
中期底 :   DRAW_调整幅度_L || _DRAW_调整幅度L       COLORCYAN NODRAW;









{ ----------------------------------------------------------------------------------------------- 大盘 - 顶/底 }
大盘顶底_STATUS :=     大盘顶底.大盘顶底_STATUS       COLORWHITE NODRAW;


NOTEXT_大盘底 :   大盘顶底_STATUS=1;
NOTEXT_大盘顶 :   大盘顶底_STATUS=2;



STICKLINE(NOTEXT_大盘底 AND Z          ,0,1  ,0,3)     COLORCYAN;
STICKLINE(NOTEXT_大盘顶 AND Z          ,0,1  ,0,3)     COLORYELLOW;
